Guavascript {
    Program     = Block
    Block       = Statement+
    Statement   = Exp "?" Block ":" Block                         -- conditionalStatement
                | Exp                                             -- expStatement
                | "class" id "{" Block "}"                        -- classStatement
                | ("match" IdExp "with" "|"? Match
                  ("|" Match)* ("|" "_" "->" Block))              -- matchStatement
                | "if" Exp "{" Block "}" ("else" "{" Block "}")?  -- ifElseStatement
                | "while" Exp "{" Block "}"                       -- whileStatement
                | "for" id "in" IdExp "{" Block "}"               -- forInStatement
    Match       = Exp "->" Block

    Exp         = id assignOp BoolOrExp                           -- assignExp
                | BoolOrExp
    BoolOrExp   = BoolOrExp orOp BoolAndExp                       -- boolOrExp
                | BoolAndExp
    BoolAndExp  = BoolAndExp andOp RelExp                         -- boolAndExp
                | RelExp
    RelExp      = AddExp relOp AddExp                             -- relExp
                | AddExp
    AddExp      = AddExp addOp MulExp                             -- addExp
                | MulExp
    MulExp      = MulExp mulOp ExponExp                           -- mulExp
                | ExponExp
    ExponExp    = PrefixExp exponOp AddExp                        -- exponExp
                | PrefixExp
    PrefixExp   = prefixOp? Exp1
    Exp1        = VarExp
                | "(" Exp ")"                                     -- parenExp
    VarExp      = (IdExp | List | Tuple | Dictionary | boolLit
                | intLit | floatLit | stringLit)

    IdExp       = id ("." id)* (Arguments | IdSelector)* idPostOp?
    Arguments   = "(" (VarExp ("," VarExp)*)? ")"
    IdSelector  = "[" VarExp "]"
    idPostOp    = "++" | "--"

    List        = "[" (VarExp ("," VarExp)*)? "]"
    Tuple       = "(" (VarExp ("," VarExp)*)? ")"
    Dictionary  = "{" (IdValuePair ("," IdValuePair)*)? "}"
    IdValuePair = id ":" VarExp

    orOp        = "||"
    andOp       = "&&"
    exponOp     = "**"
    assignOp    = "=" | "+=" | "-=" | "*=" | "/="
    addOp       = "+" | "-"
    mulOp       = "*" | "//" | "/" | "%"
    relOp       = "<=" | "<" | "==" | "!=" | ">=" | ">"
    prefixOp    = "-" | "!" | "++" | "--"

    boolLit     = "True" | "False"
    intLit      = ~floatLit digit+
    floatLit    = digit+ "." digit+
    stringLit   = "\"" alnum+ "\""

    keyword     = ("class" | "true" | "false" | "match" | "with" | "if"
                | "else" | "while" | "for" | "in" | "this") ~idrest
    id          = ~keyword letter idrest*
    idrest      = "_" | alnum

    space      += comment
    comment     = "#" (~"\n" any)* "\n"
}
