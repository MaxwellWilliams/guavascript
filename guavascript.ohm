Guavascript {
    Program     = Block
    Block       = Statement+
    Statement   = Exp "?" Block ":" Block                         -- conditionalStatement
                | id "(" (Param ("," Param)*)? ")"
                  "{" Block "}"  -- funcDeclStatement
                | "class" classId "{" Block "}"                         -- classDeclStatement
                | MatchExp                                       -- matchStatement
                | "if" Exp "{" Block "}" ("else" "{" Block "}")?  -- ifElseStatement
                | "while" Exp "{" Block "}"                          -- whileStatement
                | "for" id "in" IdExp "{" Block "}"                  -- forInStatement
                | "print" "(" Exp ")"                                   -- printStatement
                | Assign                                                -- assignStatement
                | IdExp                                                 -- IdExp
                | "ret" Exp                                             -- ReturnStatement
    Param       = id ("=" Var)?
    MatchExp    = ("match" IdExp "with" "|"? Match
                  ("|" Match)* ("|" "_" "->" (Block | Var))?)
    Match       = Exp "->" (Block | Var)

    Assign      = id assignOp (Exp | MatchExp)                         -- IdExp
    Exp         = Exp orOp BoolAndExp                                  -- boolOrExp
                | BoolAndExp
    BoolAndExp  = BoolAndExp andOp RelExp                              -- boolAndExp
                | RelExp
    RelExp      = AddExp relOp AddExp                                  -- relExp
                | AddExp
    AddExp      = AddExp addOp MulExp                                  -- addExp
                | MulExp
    MulExp      = MulExp mulOp ExponExp                                -- mulExp
                | ExponExp
    ExponExp    = PrefixExp exponOp AddExp                             -- exponExp
                | PrefixExp
    PrefixExp   = prefixOp? Exp1
    Exp1        = Var
                | "(" Exp ")"                                          -- parenExp
    Var         = (boolLit | IdExp | List | Tuple | Dictionary
                | intLit | floatLit | stringLit)

    IdExp       = id (periodId | Arguments | IdSelector)* idPostOp?
    periodId    = "." id
    Arguments   = "(" (Var ("," Var)*)? ")"
    IdSelector  = "[" Var "]"
    idPostOp    = "++" | "--"

    List        = "[" (Var ("," Var)*)? "]"
    Tuple       = "(" (Var ("," Var)*)? ")"
    Dictionary  = "{" (IdValuePair ("," IdValuePair)*)? "}"
    IdValuePair = id ":" Var

    orOp        = "||"
    andOp       = "&&"
    exponOp     = "^"
    assignOp    = "=" | "+=" | "-=" | "*=" | "/="
    addOp       = "+" | "-"
    mulOp       = "*" | "//" | "/" | "%"
    relOp       = "<=" | "<" | "==" | "!=" | ">=" | ">"
    prefixOp    = "--" | "-" | "!" | "++"

    boolLit     = "True" | "False"
    intLit      = ~floatLit digit+
    floatLit    = digit+ "." digit+
    stringLit   = "\"" ("\\\"" | (~"\"" any))* "\""

    keyword     = ("class" | "true" | "false" | "match" | "with" | "if"
                | "else" | "while" | "for" | "in" | "this" | "print" | "ret") ~idrest
    id          = ~keyword lower idrest*                               -- identifier
                | constId
    idrest      = "_" | alnum
    constId     = ~keyword upper ("_" | upper)*
    classId     = upper idrest*

    space      += comment
    comment     = "#" (~"\n" any)* "\n"
}
